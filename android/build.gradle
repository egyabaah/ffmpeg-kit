buildscript {
  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:8.1.0'
  }
}

apply plugin: 'com.android.library'

static def safePackageName(String packageName) {
  packageName.replace("-lts", "")
}

def safePackageVersion(String packageName) {
  def version = project.properties['ffmpegKit.android.main.version']
  def ltsVersion = project.properties['ffmpegKit.android.lts.version']
  packageName.contains("-lts") ? ltsVersion + ".LTS" : version
}

def safeExtGet(String prop, String fallback) {
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
  if (project.android.hasProperty("namespace")) {
    namespace 'com.arthenica.ffmpegkit.reactnative'
  }

  compileSdkVersion 33

  defaultConfig {
    minSdkVersion safeExtGet('ffmpegKitPackage', 'https').contains("-lts") ? 16 : 24
    targetSdkVersion 33
    versionCode 602
    versionName "6.0.2"
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }
  lintOptions {
    disable 'GradleCompatible'
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_11
    targetCompatibility JavaVersion.VERSION_11
  }
}

// âœ… Add local repo in `android/app/repo/`
// NB: Dont forget to add a maven module to the path eg: app/repo/com/local/ffmpeg-kit/audio_binary/6.0-2
// It is recommended to create the module using mvn command see https://github.com/arthenica/ffmpeg-kit/issues/1136
rootProject.allprojects { Project subproject ->
  subproject.repositories {
    maven { url file("${rootProject.project(':app').projectDir}/repo") }
  }
}

repositories {
  mavenCentral()
  google()

  def found = false
  def defaultDir = null
  def androidSourcesName = 'React Native sources'

  if (rootProject.ext.has('reactNativeAndroidRoot')) {
    defaultDir = rootProject.ext.get('reactNativeAndroidRoot')
  } else {
    defaultDir = new File(
      projectDir,
      'node_modules/react-native/android'
    )
  }

  if (defaultDir.exists()) {
    maven {
      url defaultDir.toString()
      name androidSourcesName
    }

    logger.info(":${project.name}:reactNativeAndroidRoot ${defaultDir.canonicalPath}")
    found = true
  } else {
    def parentDir = rootProject.projectDir

    1.upto(5, {
      if (found) return true
      parentDir = parentDir.parentFile

      def androidSourcesDir = new File(
        parentDir,
        'node_modules/react-native'
      )

      def androidPrebuiltBinaryDir = new File(
        parentDir,
        'node_modules/react-native/android'
      )

      if (androidPrebuiltBinaryDir.exists()) {
        maven {
          url androidPrebuiltBinaryDir.toString()
          name androidSourcesName
        }

        logger.info(":${project.name}:reactNativeAndroidRoot ${androidPrebuiltBinaryDir.canonicalPath}")
        found = true
      } else if (androidSourcesDir.exists()) {
        maven {
          url androidSourcesDir.toString()
          name androidSourcesName
        }

        logger.info(":${project.name}:reactNativeAndroidRoot ${androidSourcesDir.canonicalPath}")
        found = true
      }
    })
  }

  if (!found) {
    throw new GradleException(
      "${project.name}: unable to locate React Native android sources. " +
        "Ensure you have you installed React Native as a dependency in your project and try again."
    )
  }
}

dependencies {
  api 'com.facebook.react:react-native:+'
  // will result in something like: implementation 'com.local.ffmpeg-kit:audio_binary:6.0-2'
  implementation 'com.local.ffmpeg-kit:' + safePackageName(safeExtGet('ffmpegKitPackage', 'https')) + '_binary:' + safePackageVersion(safeExtGet('ffmpegKitPackage', 'https'))
  // Required: Place smart-exception-java-0.2.1.jar in android/app/repo/smart-exception-java-0.2.1.jar
  implementation files("${rootProject.project(':app').projectDir}/repo/smart-exception-java-0.2.1.jar")
  // Required: Place smart-exception-common-0.2.1.jar in android/app/repo/smart-exception-common-0.2.1.jar
  implementation files("${rootProject.project(':app').projectDir}/repo/smart-exception-common-0.2.1.jar")
  
}
